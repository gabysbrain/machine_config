# Set vi keys
bindkey -v

# Set the prompt so its pretty
set prompt = "[%n@%m %c02][%h]>"

# Setup my path
set path
set path = ( $path /usr/local/sbin )
set path = ( $path /sbin )
set path = ( $path /usr/sbin )
set path = ( $path /usr/local/bin )
set path = ( $path /bin )
set path = ( $path /usr/bin )
set path = ( $path /usr/share/java/apache-ant/bin )
set path = ( $path . )

# Set the PAGER
setenv PAGER less

# Set the EDITOR
setenv EDITOR vim

# Shell Variables
set addsuffix	
set autocorrect	
set autoexpand	
set autolist
set cdpath = ~/
set complete = enhance
set correct = cmd
set dextract	
set dunique	
set echo_style = bsd
set edit	
set fignore	= (~ .bak .o .bin RCS CVS)
set histdup	= erase
set history	= 150
set inputmode = insert
set interactive	
set listjobs = long
set matchbeep = never
set nokanji	
set nostat = (/afs /net /Net /Network/Servers)
set savehist = 150 merge
set symlinks = ignore

# Personal Variables
set machines = (macs.dnsdojo.net blitz.cs.pitt.edu elements.cs.pitt.edu)
set usernames = (torsneyt tom)

# Personal Environment Variables
setenv DISPLAY :0.0

# Aliases
alias gvim  'gvim -X'
alias vi    'vim'
alias .     'pwd'
alias ..    'cd ..'
alias cd..  'cd ..'
alias cdwd  'cd `pwd`'
alias cwd   'echo $cwd'
alias files 'find \!:1 -type f -print'  # files x => list files in x
alias ff    'find . -name \!:1 -print'  # ff x => find file named x
alias line  'sed -n '\''\!:1 p'\'' \!:2'    # line 5 file => show line 5 of file
alias l     'ls -l'
alias ll    'ls -la \!* | $PAGER'
alias ls	'ls -F'
alias m 	'$PAGER'
alias h		'history'
alias j		'jobs'
alias pu	'pushd'
alias po	'popd'
alias term  'set noglob; unsetenv TERMCAP; eval `tset -s -I -Q - \!*`'
alias word  'grep \!* /usr/share/dict/web2' # Grep thru dictionary
alias wordcount '(cat \!* | tr -s '\''  .,;:?\!()[]"'\'' '\''\012'\'' |' \
        'cat -n | tail -1 | awk '\''{print $1}'\'')' # Histogram words

# Completions
complete alias	'p/1/a/' 'p/2/c/'
complete bindkey	'p/2/b/'
complete chgrp	'p/1/g/' 'n/-R/g/' 'p/*/f/'
complete chmod	'p/1/(u g o a)//' 'n/-R/(u g o a)//'
complete chown	'p/1/u/' 'n/-R/u/' 'p/*/f/'
complete complete	'p/1/X/'
complete dd	'c/--/(help version)/' 'c/[io]f=/f/' 'c/conv=*,/(ascii ebcdic ibm block unblock lcase ucase swab noerror sync)/,' 'c/conv=/(ascii ebcdic ibm block unblock lcase ucase swab noerror sync)/,' 'c/*=/x:<number>/' 'n/*/(if of conv ibs obs bs cbs files skip file seek count)/='
complete defaults	'c/-/(currentHost host)/' 'n/-host/`list_all_hostnames`/' 'n/read/`defaults domains`/' 'n/read-type/`defaults domains`/' 'n/write/`defaults domains`/' 'n/rename/`defaults domains`/' 'n/delete/`defaults domains`/' 'p/1/(read read-type write rename delete domains find help)/'
complete exec	'p/1/c/'
complete files	'p/1/d/'
complete find	'n/-fstype/(nfs 4.2)/' 'n/-name/f/' 'n/-user/u/' 'n/-group/g/' 'n/-type/(c b d f p l s)/' 'n/-exec/c/' 'n/-ok/c/' 'n/-cpio/f/' 'n/-ncpio/f/' 'n/-newer/f/' 'c/-/(fstype name perm prune type user nouser group	nogroup size inum atime mtime ctime exec ok	print ls cpio ncpio newer xdev depth daystart	follow maxdepth mindepth noleaf version anewer	cnewer amin cmin mmin true false uid gid ilname	iname ipath iregex links lname empty path regex	used xtype fprint fprint0 fprintf print0 printf	not a and o or)/' 'n/*/d/'
complete finger	'c/*@/`list_all_hostnames`/' 'n/*/u/@'
complete gdb	'c/-cd=/d/' 'n/-b/n/' 'c@-tty=@`ls -1 /dev/tty*`@' 'c/-symbols=/f/' 'n/-s/f/' 'c/-exec=/c/' 'n/-e/c/' 'n/-se/c/' 'c/-command=/f/' 'n/-x/f/' 'c/-core=/f/' 'n/-c/f/' 'c/-directory=/d/' 'n/-d/d/' 'c/-/(help nx n q batch b s se write e c x d quiet q fullname f)/' 'n/*/c/'
complete gunzip	'p/*/f:*.{gz,tgz}/'
complete javac	'n/*/f:*.java/'
complete kill	'c/%/j/' 'c/-/S/' 'p/1/(% -)/'
complete man	'n/-M/d/' 'p/*/c/'
complete nicl	'c/-/(ro p u P raw t q x500	read create delete rename list append merge insert	move copy search path setrdn history stats domainname	rparent resync auth refs)/' 'n/-u/u/' 'n/-auth/u/' 'n/-P/n/' 'n/-raw/d/'
complete printenv	'p/1/e/'
complete rlogin	'p/1/`list_all_hostnames`/' 'c/-/(l 8 e)/' 'n/-l/u/'
complete rm	'p/*/f/' 'c/-/(r f)/'
complete rsh	'p/1/`list_all_hostnames`/' 'c/-/(l n)/' 'n/-l/u/' 'N/-l/c/' 'n/-/c/' 'p/2/c/' 'p/*/f/'
complete set	'c/*=/f/' 'p/1/s/=' 'n/=/f/'
complete setenv	'p/1/e/'
complete ssh	'n/-l/$usernames/' 'p/*/$machines/'
complete su	'c/-/(f l m K)/' 'p/1/u/' 'n/-[flmK]/u/'
complete sudo	'n/-u/u/' 'n/-p/n/' 'n/-V/n/' 'n/-c/(-)/' 'n/-H/d/' 'c/-/(V h l L v k K s H S b p c u -)/' 'p/1/c/' 'n/-[hlLvkKsSb-]/c/' 'N/-[upcH]/c/'
complete unalias	'p/*/a/'
complete uncomplete	'n/*/X/'
complete unset	'p/*/s/'
complete unsetenv	'p/*/e/'
complete unzip	'p/*/f:*.zip/'
complete {cd,chdir,popd,pushd}	'p/1/d/'
complete {configure,./configure}	'c@{--target=,--host=}@`cat /tmp/architectures`@' 'c/--/(help quiet silent version no-create	target= build= host=	prefix= exec-prefix= bindir= sbindir= libdir= libexecdir=	includedir= infodir= mandir= srcdir=	datadir= sysconfdir= sharedstatedir= localstatedir=	program-prefix= program-suffix= program-transform-name=	x-includes= x-libraries=	with-cc= with-ccopts= with-linker= with-ldopts= with-cppopts=	disable- enable- enable-shared with- without-)//'
complete {fg,bg,stop}	'c/%/j/' 'p/1/(%)//'
complete {ftp,ncftp,telnet}	'p/*/`list_all_hostnames`/'
complete {limit,unlimit}	'c/-/(h)/' 'n/*/l/'
complete {lynx,netscape,wget}	'p|1|(ftp:// gopher:// http:// https:// mailto: news: wais:// )||'
complete {mkdir,rmdir}	'p/1/d/'
complete {tar,gtar,gnutar}	'p/1/(xvf cvf tvf)//' 'p/2-/f/'
complete {trace,strace}	'p/1/c/'
complete {uncompress,zcat,gzcat}	'p/*/f:*.{Z,gz}/'
complete {where,which}	'p/1/c/'
